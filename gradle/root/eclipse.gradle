/*
 * Copyright 2012 Aluminum project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

File projectDescriptor = project.file('gradle/.project')

task cleanEclipse(type: Delete) {
	delete projectDescriptor
}

task eclipse {
	doLast {
		groovy.xml.MarkupBuilder builder = new groovy.xml.MarkupBuilder(new FileWriter(projectDescriptor))

		builder.projectDescription {
			builder.name(project.name)

			comment()
			projects()
			buildSpec()
			natures()

			linkedResources {
				['build.gradle', 'settings.gradle'].each {String buildFile ->
					link {
						builder.name(buildFile)
						type(1)
						location("PARENT-1-PROJECT_LOC/${buildFile}")
					}
				}
			}

			filteredResources {
				filter {
					builder.name()
					type(10)
					matcher {
						id('org.eclipse.ui.ide.multiFilter')
						arguments([1.0, 'name-matches', false, false, 'wrapper'].join('-'))
					}
				}
			}
		}
	}
}